dnl# $Id$

test "x$LIBTOOLIZE" = "x" && LIBTOOLIZE=true
test "x$AUTOHEADER" = "x" && AUTOHEADER=true
test "x$ACLOCAL" = "x" && ACLOCAL=true
test "x$AUTOMAKE" = "x" && AUTOMAKE=true
test "x$AUTOCONF" = "x" && AUTOCONF=true

AC_SUBST(LIBTOOLIZE)
AC_SUBST(AUTOHEADER)
AC_SUBST(ACLOCAL)
AC_SUBST(AUTOMAKE)
AC_SUBST(AUTOCONF)

SAVE_LIBTOOLIZE="$LIBTOOLIZE"
SAVE_AUTOHEADER="$AUTOHEADER"
SAVE_ACLOCAL="$ACLOCAL"
SAVE_AUTOMAKE="$AUTOMAKE"
SAVE_AUTOCONF="$AUTOCONF"

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(libast, 0.6)
AC_DEFINE_UNQUOTED(LIBAST_VERSION, "$VERSION", [Version])
AM_CONFIG_HEADER(config.h include/libast/sysdefs.h)

LIBTOOLIZE="${SAVE_LIBTOOLIZE:-true}"
AUTOHEADER="${SAVE_AUTOHEADER:-true}"
ACLOCAL="${SAVE_ACLOCAL:-true}"
AUTOMAKE="${SAVE_AUTOMAKE:-true}"
AUTOCONF="${SAVE_AUTOCONF:-true}"

AC_MSG_CHECKING(for preferred libtoolize)
AC_MSG_RESULT($LIBTOOLIZE)
AC_MSG_CHECKING(for preferred autoheader)
AC_MSG_RESULT($AUTOHEADER)
AC_MSG_CHECKING(for preferred aclocal)
AC_MSG_RESULT($ACLOCAL)
AC_MSG_CHECKING(for preferred automake)
AC_MSG_RESULT($AUTOMAKE)
AC_MSG_CHECKING(for preferred autoconf)
AC_MSG_RESULT($AUTOCONF)

dnl# Set some basic variables
AUTHORS="Michael Jennings (mej@eterm.org)"
AC_SUBST(AUTHORS)
AC_DEFINE_UNQUOTED(AUTHORS, "$AUTHORS", [Authors])

AC_PROG_CC

AST_STD_CHECKS()
AST_PROG_CHECKS()
AST_VAR_CHECKS()
AST_HEADER_CHECKS()
AST_TYPE_CHECKS()
AST_FUNC_CHECKS()

AST_ARG_DEBUG(DEBUG)
if test $DEBUG -eq 0 ; then
    AC_WARN(*** Debugging support disabled.  Client programs will ***)
    AC_WARN(*** not have access to any libast debugging routines! ***)
fi

AC_ARG_WITH(pedantry,
    [  --with-pedantry         add -Werror -std=c99 -pedantic to CFLAGS],
    [
        if test "$withval" != "no"; then
            CFLAGS="$CFLAGS -Wall -Werror -std=c99 -pedantic"
            AC_DEFINE(STRICT_ISO_C99, 1, [Defined if strict ISO C99 (9899:1999) is requested.])
        fi
    ]
)

AST_X11_SUPPORT()
AST_IMLIB2_SUPPORT()
AST_MMX_SUPPORT()
AST_ARG_REGEXP(REGEXP)
AST_ARG_BACKQUOTE_EXEC(ALLOW_BACKQUOTE_EXEC)

AST_FLAGS()
AC_SUBST(CC)
AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(LIBS)

basedir=.
AC_OUTPUT(${srcdir}/include/libast/types.h Makefile include/Makefile \
          include/libast/Makefile src/Makefile test/Makefile \
          libast-config)
test -f libast-config && chmod 755 libast-config

AST_STATUS()
