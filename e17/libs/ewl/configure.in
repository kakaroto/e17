dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AM_INIT_AUTOMAKE(ewl, 0.0.4.007)
AM_CONFIG_HEADER(ewl-config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_C_CONST
AM_ENABLE_SHARED
AM_PROG_LIBTOOL
AC_C___ATTRIBUTE__
AC_FUNC_ALLOCA

dnl Check for Solaris or other OS
case "$host" in
     *-sun-*)
        AC_DEFINE(HAVE_SUN, 1, [Support for SunOS/Solaris])
        ;;
     *) ;;
esac

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${PACKAGE}/locale", [Locale data directory])
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${PACKAGE}/locale", [Locale data directory])
fi
							
dnl Set PACKAGE_DATA_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}", [Shared Data Directory])
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}", [Shared Data Directory])
fi

dnl Set PACKAGE_LIB_DIR in config.h.
if test "x${libdir}" = 'x${exec_prefix}/lib'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_LIB_DIR, "${ac_default_prefix}/lib/${PACKAGE}", [Library module directory])
  else
    AC_DEFINE_UNQUOTED(PACKAGE_LIB_DIR, "${prefix}/lib/${PACKAGE}", [Library module directory])
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_LIB_DIR, "${libdir}/${PACKAGE}", [Library module directory])
fi

dnl Set PACKAGE_SOURCE_DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}", [Source code directory])

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

PCFLAGS=$CFLAGS

AC_PATH_GENERIC(evas, 0.9.9,
  [ ],
  [
AC_MSG_ERROR([
ERROR:
The evas-config development script was not found in your execute
path. This may mean one of several things
1. You may not have installed the evas-devel (or evas-dev) packages.
2. You may have evas installed somewhere not covered by your path.

If this is the case make sure you have the packages installed, AND
that the evas-config script is in your execute path (see your
shell's manual page on setting the \$PATH environment variable), OR
alternatively, specify the script to use with --with-evas-config.
])
  ]
)
CFLAGS=$EVAS_CFLAGS" "$CFLAGS

AC_PATH_GENERIC(ecore, 0.9.9,
  [ ],
  [
AC_MSG_ERROR([
ERROR:
The ecore-config development script was not found in your execute
path. This may mean one of several things
1. You may not have installed the ecore-devel (or ecore-dev) packages.
2. You may have ecore installed somewhere not covered by your path.

If this is the case make sure you have the packages installed, AND
that the ecore-config script is in your execute path (see your
shell's manual page on setting the \$PATH environment variable), OR
alternatively, specify the script to use with --with-ecore-config.
])
  ]
)
CFLAGS=$ECORE_CFLAGS" "$CFLAGS

AC_ARG_ENABLE(software-x11,
	[AC_HELP_STRING([--enable-software-x11],[enable software X11 support])],
	[enable_software_x11=$enableval], [enable_software_x11="auto"])
if test "x$enable_software_x11" != "xno" ; then
	have_software_x11=no
	AC_CHECK_HEADER(Evas_Engine_Software_X11.h,
		[AC_CHECK_HEADER(Ecore_X.h,
			[have_software_x11=yes],
			[], [#include <Ecore.h>])],
		[], [#include <Evas.h>])
	if test "x$have_software_x11" = "xyes" ; then
		AC_DEFINE(ENABLE_EWL_SOFTWARE_X11, 1, [Enable X11 Software])
	elif test "x$enable_software_x11" = "xyes" ; then
		AC_MSG_ERROR(software X11 requested but no evas/ecore support found)
	fi
fi
AM_CONDITIONAL(EWL_ENABLE_EVAS_SOFTWARE_X11, test "x$have_software_x11" = xyes)

AC_ARG_ENABLE(opengl-x11,
	[AC_HELP_STRING([--enable-opengl-x11],[enable opengl X11 support])],
	[enable_opengl_x11=$enableval], [enable_opengl_x11="auto"])
if test "x$enable_opengl_x11" != "xno" ; then
	have_opengl_x11=no
	AC_CHECK_HEADERS(Evas_Engine_GL_X11.h,
		[AC_CHECK_HEADER(Ecore_X.h,
			[have_opengl_x11=yes],
			[], [#include <Ecore.h>])],
		[], [#include <Evas.h>])
	if test "x$have_opengl_x11" = "xyes" ; then
		AC_DEFINE(ENABLE_EWL_GL_X11, 1, [Enable OpenGL X11])
	elif test "x$enable_opengl_x11" = "xyes" ; then
		AC_MSG_ERROR(opengl X11 requested but no evas/ecore support found)
	fi
fi
AM_CONDITIONAL(EWL_ENABLE_EVAS_GL_X11, test "x$have_opengl_x11" = xyes)

AC_ARG_ENABLE(fbcon,
	[AC_HELP_STRING([--enable-fbcon],[enable fbcon support])],
	[enable_fbcon=$enableval], [enable_fbcon="auto"])
if test "x$enable_fbcon" != "xno" ; then
	have_fbcon=no
	AC_CHECK_HEADERS(Evas_Engine_FB.h,
		[AC_CHECK_HEADER(Ecore_Fb.h,
			[have_fbcon=yes],
			[], [#include <Ecore.h>])],
		[], [#include <Evas.h>])
	if test "x$have_fbcon" = "xyes" ; then
		AC_DEFINE(ENABLE_EWL_FB, 1, [Enable FB])
	elif test "x$enable_fbcon" = "xyes" ; then
		AC_MSG_ERROR(fbcon requested but no evas/ecore support found)
	fi
fi
AM_CONDITIONAL(EWL_ENABLE_EVAS_FB, test "x$have_fbcon" = xyes)

if test "x$have_software_x11" = "xyes"; then
	AM_CONDITIONAL(EWL_ENABLE_X11, true)
else
	if test "x$have_opengl_x11" = "xyes"; then
		AM_CONDITIONAL(EWL_ENABLE_X11, true)
	else
		AM_CONDITIONAL(EWL_ENABLE_X11, false)
	fi
fi

AC_PATH_GENERIC(edje, 0.5.0,
  [ ],
  [
AC_MSG_ERROR([
ERROR:
The edje-config development script was not found in your execute
path. This may mean one of several things
1. You may not have installed the edje-devel (or edje-dev) packages.
2. You may have edje installed somewhere not covered by your path.

If this is the case make sure you have the packages installed, AND
that the edje-config script is in your execute path (see your
shell's manual page on setting the \$PATH environment variable), OR
alternatively, specify the script to use with --with-edje-config.
])
  ]
)

requirements="evas ecore edje"

CFLAGS=$EDJE_CFLAGS" "$CFLAGS
AC_PATH_GENERIC(emotion, 0.0.1,
  [ 
    have_emotion="yes"
    ewl_media_include="#include <ewl_media.h>"
    requirements="$requirements emotion"
    AC_DEFINE(BUILD_EMOTION_SUPPORT, 1, [Enable Emotion Support for Ewl_Media])
  ],
  [
    have_emotion="no"
  ]
)

CFLAGS=$EMOTION_CFLAGS" "$CFLAGS

AC_PATH_GENERIC(epsilon, 0.3.0,
  [ 
    have_epsilon="yes"
    requirements="$requirements epsilon"
    AC_DEFINE(BUILD_EPSILON_SUPPORT, 1, [Enable Epsilon Support for Ewl_Image])
  ],
  [
    have_epsilon="no"
  ]
)

CFLAGS=$EPSILON_CFLAGS" "$CFLAGS

###

CFLAGS=$PCFLAGS

AC_SUBST(ewl_media_include)

AC_SUBST(requirements)

AC_OUTPUT([
Makefile
ewl.spec
ewl.pc
ewl-config
src/Makefile
src/bin/Makefile
src/bin/tests/Makefile
src/lib/Makefile
src/engines/Makefile
src/engines/x11/Makefile
src/engines/evas/Makefile
src/engines/evas_software_x11/Makefile
src/engines/evas_gl_x11/Makefile
src/engines/evas_fb/Makefile
data/Makefile
data/images/Makefile
data/themes/Makefile
debian/changelog
], [
chmod +x ewl-config
]
)
