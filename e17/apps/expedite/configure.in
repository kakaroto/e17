dnl Process this file with autoconf to produce a configure script.

# get rid of that stupid cache mechanism
rm -f config.cache

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(expedite, 0.6.0)
AM_CONFIG_HEADER(config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_C_CONST
AM_ENABLE_SHARED
AM_PROG_LIBTOOL

if test "x${bindir}" = 'xNONE'; then
  if test "x${prefix}" = "xNONE"; then
    PACKAGE_BIN_DIR="${ac_default_prefix}/bin"
  else
    PACKAGE_BIN_DIR="${prefix}/bin"
  fi
else
  PACKAGE_BIN_DIR="${bindir}"
fi
AC_SUBST(PACKAGE_BIN_DIR)
		
if test "x${libdir}" = 'xNONE'; then
  if test "x${prefix}" = "xNONE"; then
    PACKAGE_LIB_DIR="${ac_default_prefix}/lib"
  else
    PACKAGE_LIB_DIR="${prefix}/lib"
  fi
else
  PACKAGE_LIB_DIR="${libdir}"
fi
AC_SUBST(PACKAGE_LIB_DIR)

if test "x${prefix}" = "xNONE"; then
  PACKAGE_DATA_DIR="${ac_default_prefix}/share/${PACKAGE}"
else
  PACKAGE_DATA_DIR="${prefix}/share/${PACKAGE}"
fi
AC_SUBST(PACKAGE_DATA_DIR)

PKG_CHECK_MODULES([EVAS], evas)

PCFLAGS=$CFLAGS
CFLAGS="$EVAS_CFLAGS $CFLAGS"

have_xlib_opengl="no"
AC_CHECK_HEADER(Evas_Engine_GL_X11.h,
  [ AC_DEFINE(HAVE_EVAS_GL, 1, [Evas GL Engine Support])
    have_xlib_opengl="yes" ],
  [ dummy=no ],
  [ #include <Evas.h> ]
)

have_xlib="no"
AC_CHECK_HEADER(Evas_Engine_Software_X11.h,
  [ AC_DEFINE(HAVE_SOFTWARE_X11, 1, [Evas Software X11 Engine Support])
    have_xlib="yes" ],
  [ dummy=no ],
  [ #include <Evas.h> ]
)

have_xlib_render="no"
AC_CHECK_HEADER(Evas_Engine_XRender_X11.h,
  [ AC_DEFINE(HAVE_XRENDER_X11, 1, [Evas XRender X11 Engine Support])
    have_xlib_render="yes" ],
  [ dummy=no ],
  [ #include <Evas.h> ]
)

have_xlib_16="no"
AC_CHECK_HEADER(Evas_Engine_Software_16_X11.h,
  [ AC_DEFINE(HAVE_SOFTWARE_16_X11, 1, [Evas Software 16bit X11 Engine Support])
    have_xlib_16="yes" ],
  [ dummy=no ],
  [ #include <Evas.h> ]
)

have_ddraw="no"
AC_CHECK_HEADER(Evas_Engine_Software_DDraw.h,
  [ AC_DEFINE(HAVE_SOFTWARE_DDRAW, 1, [Evas Software DirectDraw Engine Support])
    have_ddraw="yes"
    ddraw_libs="-lddraw -ldxguid" ],
  [ dummy=no ],
  [ #include <Evas.h> ]
)

if test "x$have_xlib" = "xyes" -o "x$have_xlib_16" = "xyes" -o "x$have_xlib_render" = "xyes" -o "x$have_xlib_opengl" = "xyes" ; then
AC_PATH_XTRA
x_dir=${x_dir:-/usr/X11R6}
x_cflags=${x_cflags:--I${x_includes:-$x_dir/include}}
x_libs="${x_libs:--L${x_libraries:-$x_dir/lib}} -lX11 -lXext"
fi

AC_SUBST(x_cflags)
AC_SUBST(x_libs)
AC_SUBST(ddraw_libs)

#######################################
## SDL
AC_ARG_WITH(sdl-config, [  --with-sdl-config=SDL_CONFIG     use sdl-config specified],
[ SDL_CONFIG=$withval;
  echo "using "$SDL_CONFIG" for sdl-config"; ],
[ if test -z "$SDL_CONFIG"; then
    AC_PATH_PROG(SDL_CONFIG, "sdl-config", "", $PATH)
  fi
])
if test -z "$SDL_CONFIG" ; then SDL_CONFIG="sdl-config"; fi

sdl_cflags=""
sdl_libs=""

have_sdl="no"
AC_CHECK_HEADER(Evas_Engine_SDL.h,
  [ AC_DEFINE(HAVE_SOFTWARE_SDL, 1, [Evas Software SDL Support])
    have_sdl="yes"
    sdl_cflags=`$SDL_CONFIG --cflags`
    sdl_libs=`$SDL_CONFIG --libs` ],
  [ dummy=no ],
  [ #include <Evas.h> ]
)

AC_SUBST(sdl_cflags)
AC_SUBST(sdl_libs)

CFLAGS=$PCFLAGS

AC_OUTPUT([
Makefile
src/Makefile
src/bin/Makefile
data/Makefile
])

echo
echo "  $PACKAGE $VERSION"
echo
echo "  Engines:"
echo "    Software Xlib...........: ${have_xlib}"
echo "    Software Xlib 16 bits...: ${have_xlib_16}"
echo "    X Render Xlib...........: ${have_xlib_render}"
echo "    Open GL Xlib............: ${have_xlib_opengl}"
echo "    Software DirectDraw.....: ${have_ddraw}"
echo "    Software SDL............: ${have_sdl}"
echo
echo "  Installation:"
echo "    prefix..................: ${prefix}"
echo
