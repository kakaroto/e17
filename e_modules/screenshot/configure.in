dnl Process this file with autoconf to produce a configure script.

# get rid of that stupid cache mechanism
rm -f config.cache

AC_INIT(screenshot, 0.2.0, devilhorns@comcast.net)
AC_PREREQ(2.52)
AC_CONFIG_SRCDIR(configure.in)
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_ISC_POSIX

AM_INIT_AUTOMAKE(1.6)
AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_C_CONST

define([AC_LIBTOOL_LANG_CXX_CONFIG], [:])dnl
define([AC_LIBTOOL_LANG_F77_CONFIG], [:])dnl
AC_PROG_LIBTOOL

AM_GNU_GETTEXT([external])
AM_GNU_GETTEXT_VERSION(0.14)

MODULE_ARCH="$host_os-$host_cpu"
AC_SUBST(MODULE_ARCH)
AC_DEFINE_UNQUOTED(MODULE_ARCH, "$MODULE_ARCH", "Module architecture")

AC_PATH_PROG(EDJE_CC, "edje_cc", "", $PATH)

AC_ARG_WITH(enlightenment-config,
  AS_HELP_STRING([--with-enlightenment-config=E_CONFIG], [use enlightenment-config specified]),
  [
    E_CONFIG=$withval;
    echo "using "$E_CONFIG" for enlightenment-config";
  ], [
    PROG="enlightenment-config";
    AC_PATH_PROG(E_CONFIG, $PROG, "", $PATH)
  ]
)
e_cflags=`$E_CONFIG --cflags`
e_libs=`$E_CONFIG --libs`
e_modules=`$E_CONFIG --module-dir`
AC_SUBST(e_cflags)
AC_SUBST(e_libs)

datadir="${e_modules}/${PACKAGE}"    
AC_ARG_ENABLE(homedir-install,
  AS_HELP_STRING([--enable-homedir-install], [Install module in homedir]),
  [ datadir="${HOME}/.e/e/modules/${PACKAGE}" ]
)

AC_PATH_PROG(IMPORT, import, "", $PATH)
if test -z "$IMPORT"; then
    AC_MSG_WARN([Import not found])
else
    AC_DEFINE_UNQUOTED(HAVE_IMPORT, 1, "You Have Import")
    AC_DEFINE_UNQUOTED(IMPORT_PATH, "$IMPORT", "You Have Import")
fi

AC_PATH_PROG(SCROT, scrot, "", $PATH)
if test -z "$SCROT"; then
    AC_MSG_WARN([Scrot not found])
else
    AC_DEFINE_UNQUOTED(HAVE_SCROT, 1, "You Have Scrot")
    AC_DEFINE_UNQUOTED(SCROT_PATH, "$SCROT", "You Have Scrot")
fi

if test -z "$IMPORT"; then
   if test -z "$SCROT"; then
      AC_MSG_ERROR("You do not have import or scrot installed.")
   fi
fi

AC_OUTPUT([
Makefile
e_modules-screenshot.spec
po/Makefile.in
], [
])
